plugins {
    id 'jacoco'
    id 'java-library'
    id 'checkstyle'
    id 'pmd'
}

defaultTasks 'build'

allprojects {
    apply plugin: 'idea'

    repositories {
        jcenter()
    }
}


subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    group 'be.xplore'

    project.plugins.withType(JavaBasePlugin) {
        project.with {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.3.2.RELEASE')
        testImplementation platform('org.springframework.boot:spring-boot-dependencies:2.1.9.RELEASE')
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.assertj:assertj-core:3.13.2'
        testImplementation 'org.mockito:mockito-core:3.1.0'
    }

    pmd {
        consoleOutput = true
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
        ruleSets = []
        toolVersion = '6.17.0'
        incrementalAnalysis = true
    }

    jacoco {
        toolVersion = '0.8.4'
    }

    checkstyle {
        configFile = file("${rootProject.rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = '8.23'
    }

    idea {
        module {
            downloadJavadoc = true
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn = subprojects.build
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = true
        csv.enabled = false
    }
}

rootProject.check.dependsOn codeCoverageReport
