plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    group 'be.xplore'

    repositories {
        mavenCentral()
    }

    pmd {
        consoleOutput = true
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
        ruleSets = []
        toolVersion = '6.15.0'
    }

    checkstyle {
        configFile = file("${rootProject.rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = '8.22'
    }

    idea {
        module {
            downloadJavadoc = true
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

subprojects {

    project.plugins.withType(JavaBasePlugin) {
        project.with {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }
    }
}

jacoco {
    toolVersion = '0.8.4'
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = false
        csv.enabled = false
    }
}

rootProject.check.dependsOn codeCoverageReport
