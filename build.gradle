plugins {
    id 'jacoco'
    id 'java'
    id 'checkstyle'
    id 'pmd'
}

defaultTasks 'build'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    group 'be.xplore'

    repositories {
        jcenter()
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java'

    project.plugins.withType(JavaBasePlugin) {
        project.with {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile("org.assertj:assertj-core:3.12.2")
    }

    pmd {
        consoleOutput = true
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
        ruleSets = []
        toolVersion = '6.15.0'
    }

    jacoco {
        toolVersion = '0.8.4'
    }

    checkstyle {
        configFile = file("${rootProject.rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = '8.22'
    }

    idea {
        module {
            downloadJavadoc = true
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn = subprojects.build
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = true
        csv.enabled = false
    }
}

rootProject.check.dependsOn codeCoverageReport
